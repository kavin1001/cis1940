# IO

## The problem with purity

Remember that Haskell is a _pure_ language. This means two primary things:

1. Functions may not have any external effects. For example, a function may not print anything on the screen. Functions may only compute their outputs.

2. Functions may not depend on external stuff. For example, they may not read from the keyboard, or filesystem, or network. Functions may depend only on their inputs — put another way, functions should give the same output for the same input every time.

But sometimes we do want to be able to do stuff like this! In fact, these things are possible in Haskell, but it looks very different than in most other languages.

## The IO type

The solution is a special type called `IO`. Values of type `IO a` are _descriptions_ of effectful computations, which, if executed would (possibly) perform some effectful input/output operations and (eventually) produce a value of type `a`. 

There is a level of indirection here that’s crucial to understand. A value of type `IO a`, in and of itself, is just an inert, perfectly safe thing with no effects. It is just a description of an effectful computation. One way to think of it is as a _first-class imperative program_.

As an illustration, suppose you have

```Haskell
c :: Cake
```

What do you have? A cake! Plain and simple. Suppose instead you have 

```Haskell
r :: Recipe Cake
```

What do you have? A cake? No, you have some _instructions_ for how to make a cake.

Not only do you not actually have a cake, merely being in possession of the recipe has no effect on anything else whatsoever. Simply holding the recipe in your hand does not cause your oven to get hot or flour to be spilled all over your floor or anything of that sort. To actually produce a cake, the recipe must be followed (causing the oven to get hot, the flour to spill, and so on).

In the same way, a value of type `IO a` is just a "recipe" for producing a value of type `a` (and possibly having some effects along the way). Like any other value, it can be passed as an argument, returned as the output of a function, stored in a data structure, or (as we will see shortly) combined with other `IO` values into more complex recipes.

So, how do values of type `IO a` actually ever get executed? The Haskell compiler looks for a special value

```Haskell
main :: IO ()
```

which will actually get handed to the runtime system and executed.

So let’s write our first actual, executable Haskell program! We can use the function

```Haskell
putStrLn :: String -> IO ()
```

which, given a `String`, returns an `IO` computation that will (when executed) print out that `String` on the screen. So we simply put this in a file called `Hello.hs`:

```Haskell
main :: IO ()
main = putStrLn "Hello, Haskell!"
```
Then typing `runhaskell Hello.hs` at a command-line prompt results in our message getting printed to the screen.

## There is no `String` "inside" an `IO String`

Many new Haskell users end up at some point asking a question like "I have an `IO String`, how do I turn it into a `String`?" — given the above intuition, it should be clear that this question does not make sense. A value of type `IO String` is a description of some computation, a recipe, for generating a `String`. There is no `String` "inside" an `IO String`, any more than there is a cake "inside" a cake recipe. To produce a `String` (or a cake) requires actually executing the computation (or recipe).

## Combining IO

We also need a way to combine `IO` computations into larger ones.

The simplest way to combine two IO computations is with the `(>>)` operator (pronounced "and then"), which has the type

```Haskell
(>>) :: IO a -> IO b -> IO b
```

This simply creates an `IO` computation which consists of running the two input computations in sequence. Notice that the result of the first computation is discarded; we only care about it for its effects. For example:

```Haskell
main :: IO ()
main = putStrLn "Hello" >> putStrLn "world!"
```

This works fine for code of the form “do this; do this; do this” where the results don’t really matter. However, in general this is insufficient. What if we don’t want to throw away the result from the first computation?

A first attempt at resolving the situation might be to have something of type `IO a -> IO b -> IO (a, b)`. However, this is also insufficient. The reason is that we want the second computation to be able to depend on the result of the first. 

For example, suppose we want to read an integer from the user and then print out one more than the integer they entered. In this case the second computation (printing some number on the screen) will be different depending on the result of the first.

Instead, there is an operator `(>>=)` (pronounced "bind") with the type

```Haskell
(>>=) :: IO a -> (a -> IO b) -> IO b
```

This can be difficult to wrap one’s head around at first! `(>>=)` takes a computation which will produce a value of type `a`, and a function which gets to compute a second computation based on this intermediate value of type `a`. The result of `(>>=)` is a (description of a) computation which performs the first computation, uses its result to decide what to do next, and then does that.

We can implement the example situation above. Note our use of `readLn :: Read a => IO a` which is a computation that reads input from the user and converts it into any type which is an instance of `Read`.

```Haskell
main :: IO ()
main = putStr "Enter a number: " >> readLn >>= (\n -> print (n + 1))
```

Of course, this looks kind of ugly, so Haskell provides `do` notation:

```Haskell
main :: IO ()
main = do 
  putStr "Enter a number: "
  n <- readLn
  print (n + 1)
```

